// Apply the Java plugin
apply plugin: 'java'

// Use Maven (because Burp Extender is on Maven)
repositories {
    mavenCentral()
}

dependencies {
    // Add the Burp Extender interface
    // https://mvnrepository.com/artifact/net.portswigger.burp.extender/burp-extender-api
    compile group: 'net.portswigger.burp.extender', name: 'burp-extender-api', version: '2.1'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-exec
    compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    // Adds almost 6 MBs to the final jar file :(
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.30.1'
}

sourceSets {
    main {
        java {
            // Set the source directory to "src"
            srcDir 'src'
            // Exclude 'resources'
            exclude 'resources/'
        }
    }
    main {
        resources {
            // Set the resource directory to "src/resources"
            srcDir 'src/resources'
        }
    }
}

// Put the final jar file in a different location
libsDirName = '../release'

// Create a task for bundling all dependencies into a jar file.
task bigJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// Extend clean to delete eslint-security
clean.doFirst {
    println 'Deleting eslint-security'
    delete 'eslint-security'
    println 'Deleting release'
    delete 'release'
}

// Create the install task here.

// OS Detection. Source: https://stackoverflow.com/a/54315477
import org.apache.tools.ant.taskdefs.condition.Os       

// How to use example.
// task executeCommand(type: Exec) {    
//     commandLine osAdaptiveCommand('aws', 'ecr', 'get-login', '--no-include-email')
// }

private static Iterable<String> osAdaptiveCommand(String... commands) {
    def newCommands = []
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        newCommands = ['cmd', '/c']
    }

    newCommands.addAll(commands)
    return newCommands
}
// End of OS detection.



// The install task will:
// 1. Build the extension jar.
// 2. Get the git submodule in `eslint-security`.
// 3. Runs `npm-install` in `eslint-security`.
task install() {

    // 2. Build the project via gradle.
    dependsOn 'bigJar'

    // If these are not in doLast, they will be executed in the configuration
    // phase. AKA every time any task is run.
    doLast {
        println '\ngit submodule update --init --recursive --remote'
        exec {
            // 2. Get the eslint-security git submodule.
            commandLine osAdaptiveCommand('git', 'submodule', 'update', '--init', '--recursive', '--remote')
        }

        println '\ncd eslint-security & npm install'
        exec {
            // 3. Navigate to eslint-security and run npm install.
            commandLine osAdaptiveCommand('cd', 'eslint-security', '&&', 'npm', 'install')
        }
    }
}

import java.nio.file.Files
import java.nio.file.Paths
import java.io.File

task config() {
    // Check if target was provided.
    // 1. Read a command-line parameter. This will be the project path.
    if (project.hasProperty('target')) {

        String currentDir = System.properties['user.dir']
        // 2. Check if install has been called by checking for the existence of
        //    currentDir/eslint-security/node_modules. If it's not there, return
        //    an error.
        if (Files.isDirectory(Paths.get(currentDir, 'eslint-security/node_modules'))) {
            println 'eslint-security/node_modules exists'
        } else {
            throw new GradleException('eslint-security/node_modules does not exist')
        }

        // 3. Check if target is an absolute path.
        String targetDir = project.target
        File tar = new File(targetDir)
        if (!tar.isAbsolute()) {
            // If target is not absolute, concat target with the current working
            // directory and normalize it.
            targetDir = Paths.get(currentDir, targetDir).normalize()
        }
        // 4.1 Path to store extracted JavaScript files: `target/beautified`
        String beautified = Paths.get(targetDir, "beautified").toString().replace("\\", "/")

        // 4.2 Path to store ESLint results: `target/linted`
        String linted = Paths.get(targetDir, "linted").toString().replace("\\", "/")

        // 4.3 Location of the target database: `target/eslinter.sqlite`
        String db = Paths.get(targetDir, "eslinter.sqlite").toString().replace("\\", "/")

        // 4.4 Path to the eslint command: `currentDir/eslint-security/node_modules/.bin/eslint`
        String eslint = Paths.get(currentDir, 'eslint-security/node_modules/.bin/eslint').toString().replace("\\", "/")

        // 4.5 Path to the js-beautify command: `currentDir/eslint-security/node_modules/.bin/js-beautify`
        String jsbeautify = Paths.get(currentDir, 'eslint-security/node_modules/.bin/js-beautify').toString().replace("\\", "/")

        // 4.6 Detect OS. If Windows, add ".cmd" to the end of commands.
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            eslint += ".cmd"
            jsbeautify += ".cmd"
        }

        // 4.7 Path to the ESLint configuration file: `currentDir/eslint-security/configs/eslintrc-light.js`
        String cfgFile = Paths.get(currentDir, 'eslint-security/configs/eslintrc-parsia.js').toString().replace("\\", "/")

        // 5.0 Create the json file.
        String cfgStr = 
        """
{
    "beautified-javascript-path": "${beautified}",
    "eslint-output-path": "${linted}",
    "database-path": "${db}",
    "eslint-config-path": "${cfgFile}",
    "eslint-command-path": "${eslint}",
    "jsbeautify-command-path": "${jsbeautify}",
    "only-process-in-scope": false,
    "highlight": true,
    "debug": false,
    "process-tool-list": [
        "Proxy",
        "Scanner",
        "Repeater"
    ],
    "number-of-linting-threads": 1,
    "linting-timeout": 60,
    "number-of-request-threads": 10,
    "threadpool-timeout": 10,
    "lint-task-delay": 10,
    "update-table-delay": 5,
    "maximum-js-size": 0,
    "js-mime-types": [
        "application/javascript",
        "application/ecmascript",
        "application/x-ecmascript",
        "application/x-javascript",
        "text/javascript",
        "text/ecmascript",
        "text/javascript1.0",
        "text/javascript1.1",
        "text/javascript1.2",
        "text/javascript1.3",
        "text/javascript1.4",
        "text/javascript1.5",
        "text/jscript",
        "text/livescript",
        "text/x-ecmascript",
        "text/x-javascript",
        "script"
    ],
    "javascript-file-extensions": [
        "js",
        "javascript"
    ],
    "contains-javascript": [
        "text/html",
        "application/xhtml+xml"
    ],
    "removable-headers": [
        "If-Modified-Since",
        "If-None-Match"
    ]
}
        """

        File configFile = new File(Paths.get(currentDir, 'release/config.json').toString())
        configFile.write(cfgStr)

        // println "eslint = $eslint"

    } else {
        // Return an error if target is not provided.
        println 'please provide the target path like this `gradlew config -Ptarget=/path/to/target'
    }
}